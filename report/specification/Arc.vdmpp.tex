\begin{vdmpp}
/*
 * Abstract class to represent arcs between transitions and places
 */
class Arc
  instance variables
    /**
     * Place origin or destination of this arc
     */
    public place: Place
  operations
    /**
     * Returns true if the arc meets all requirements for an 'event' to happen
     *
     * @param marking mapping of places to number of tokens
     * @return boolean
     */
(*@
\label{isEnabled:17}
@*)
    public isEnabled: map Place to nat ==> bool
    isEnabled(marking) == is subclass responsibility;

    /**
     * Modifies the marking at the place of the arc
     *
     * @remark Should only be called if arc is enabled
     * @see isEnabled(marking)
     *
     * @param t transition to be triggered
     * @param m initial marking
     * @return new marking
     */
(*@
\label{trigger:30}
@*)
    public trigger: Transition * map Place to nat ==> map Place to nat
    trigger(t, m) == is subclass responsibility;
    -- pre isEnabled(m);

end Arc

/**
 * Abstract class for all input arcs: arcs from a place to a transition
 */
class InputArc is subclass of Arc

end InputArc

/**
 * Input arc that consumes all tokens at its place
 */
class ResetArc is subclass of InputArc
  operations
    /**
     * ResetArc constructor.
     *
     * @param place place connected to this arc
     */
(*@
\label{ResetArc:53}
@*)
    public ResetArc: Place ==> ResetArc
    ResetArc(p) == (
      place := p;
      return self;
    );

    /**
     * Reset arcs are always enabled
     *
     * @param marking mapping of places to number of tokens
     * @return boolean
     */
(*@
\label{isEnabled:65}
@*)
    public isEnabled: map Place to nat ==> bool
    isEnabled(marking) == (
      return true;
    )
    pre place in set dom marking;

    /**
     * Reset arcs consume all the tokens at the place
     *
     * @remark Should only be called if arc is enabled
     * @see isEnabled(marking)
     *
     * @param t transition to be triggered
     * @param m initial marking
     * @return new marking
     */
(*@
\label{trigger:80}
@*)
    public trigger: Transition * map Place to nat ==> map Place to nat
    trigger(-, m) == (
      dcl newMarking: map Place to nat := m;
      newMarking(place) := 0;
      return newMarking;
    )
    -- pre isEnabled(m)
    pre place in set dom m;

end ResetArc

/**
 * Input arc that is enabled if its place has no tokens
 */
class InhibitorArc is subclass of InputArc
  operations
    /**
     * InhibitorArc constructor.
     *
     * @param place place connected to this arc
     */
(*@
\label{InhibitorArc:100}
@*)
    public InhibitorArc: Place ==> InhibitorArc
    InhibitorArc(p) == (
      place := p;
      return self;
    );

    /**
     * Inhibitor arcs are enabled if their place is empty
     *
     * @param marking mapping of places to number of tokens
     * @return boolean
     */
(*@
\label{isEnabled:112}
@*)
    public isEnabled: map Place to nat ==> bool
    isEnabled(marking) == (
      if marking(place) = 0 then
        return true
      else
        return false;
    )
    pre place in set dom marking;

    /**
     * Inhibitor arcs do not change marking
     *
     * @remark Should only be called if arc is enabled
     * @see isEnabled(marking)
     *
     * @param t transition to be triggered
     * @param m initial marking
     * @return new marking
     */
(*@
\label{trigger:130}
@*)
    public trigger: Transition * map Place to nat ==> map Place to nat
    trigger(-, m) == (
      return m;
    )
    -- pre isEnabled(m)
    pre place in set dom m;

end InhibitorArc

/**
 * Input arc with weight
 */
class WeightedInputArc is subclass of InputArc
  instance variables
    /**
     * Amount of tokens that can 'travel' this arc
     */
    public weight: nat;
  operations
    /**
     * WeightedInputArc constructor.
     *
     * @param place place connected to this arc
     * @param weight capacity of this arc
     */
(*@
\label{WeightedInputArc:154}
@*)
    public WeightedInputArc: Place * nat ==> WeightedInputArc
    WeightedInputArc(p, wght) == (
      place := p;
      weight := wght;
      return self
    );

    /**
     * Weighted input arcs are enabled if the weight
     * is less than the number of tokens of their place
     *
     * @param marking mapping of places to number of tokens
     * @return boolean
     */
(*@
\label{isEnabled:168}
@*)
    public isEnabled: map Place to nat ==> bool
    isEnabled(marking) == (
      return marking(place) >= weight;
    )
    pre place in set dom marking;

    /**
     * Weighted input arcs remove <weight> tokens from their place
     *
     * @remark Should only be called if arc is enabled
     * @see isEnabled(marking)
     *
     * @param t transition to be triggered
     * @param m initial marking
     * @return new marking
     */
(*@
\label{trigger:183}
@*)
    public trigger: Transition * map Place to nat ==> map Place to nat
    trigger(-, m) == (
      dcl newMarking: map Place to nat := m;
      newMarking(place) := newMarking(place) - weight;
      return newMarking;
    )
    -- pre isEnabled(m)
    pre place in set dom m;

end WeightedInputArc

/**
 * Abstract class for all output arcs: arcs from a transtion to a place
 */
class OutputArc is subclass of Arc

end OutputArc

/**
 * Output arc with weight
 */
class WeightedOutputArc is subclass of OutputArc
  instance variables
   /**
    * Amount of tokens that can 'travel' this arc
    */
    public weight: nat;
  operations
    /**
     * WeightedOutputArc constructor.
     *
     * @param place place connected to this arc
     * @param weight capacity of this arc
     */
(*@
\label{WeightedOutputArc:216}
@*)
    public WeightedOutputArc: Place * nat ==> WeightedOutputArc
    WeightedOutputArc(p, wght) == (
      place := p;
      weight := wght;
      return self
    );

    /**
     * Weighted output arcs are enabled if their place
     * capacity is not overrun after moving <weight> tokens
     *
     * @param marking mapping of places to number of tokens
     * @return boolean
     */
(*@
\label{isEnabled:230}
@*)
    public isEnabled: map Place to nat ==> bool
    isEnabled(m) == (
      return m(place) + weight <= place.capacity;
    )
    pre place in set dom m;

    /**
     * Weighted output arcs moves <weight> tokens to their place
     *
     * @remark Should only be called if arc is enabled
     * @see isEnabled(marking)
     *
     * @param t transition to be triggered
     * @param m initial marking
     * @return new marking
     */
(*@
\label{trigger:245}
@*)
    public trigger: Transition * map Place to nat ==> map Place to nat
    trigger(-, m) == (
      dcl newMarking: map Place to nat := m;
      newMarking(place) := newMarking(place) + weight;
      return newMarking;
    )
    -- pre isEnabled(m)
    pre place in set dom m;

end WeightedOutputArc
\end{vdmpp}
\bigskip
\begin{longtable}{|l|r|r|r|}
\hline
Function or operation & Line & Coverage & Calls \\
\hline
\hline
\hyperref[InhibitorArc:100]{InhibitorArc} & 100&100.0\% & 2 \\
\hline
\hyperref[ResetArc:53]{ResetArc} & 53&100.0\% & 2 \\
\hline
\hyperref[WeightedInputArc:154]{WeightedInputArc} & 154&100.0\% & 19 \\
\hline
\hyperref[WeightedOutputArc:216]{WeightedOutputArc} & 216&100.0\% & 24 \\
\hline
\hyperref[isEnabled:112]{isEnabled} & 112&100.0\% & 5 \\
\hline
\hyperref[isEnabled:168]{isEnabled} & 168&100.0\% & 144 \\
\hline
\hyperref[isEnabled:230]{isEnabled} & 230&100.0\% & 100 \\
\hline
\hyperref[isEnabled:65]{isEnabled} & 65&100.0\% & 4 \\
\hline
\hyperref[isEnabled:17]{isEnabled} & 17&100.0\% & 2 \\
\hline
\hyperref[trigger:80]{trigger} & 80&100.0\% & 4 \\
\hline
\hyperref[trigger:183]{trigger} & 183&100.0\% & 83 \\
\hline
\hyperref[trigger:30]{trigger} & 30&100.0\% & 2 \\
\hline
\hyperref[trigger:245]{trigger} & 245&100.0\% & 97 \\
\hline
\hyperref[trigger:130]{trigger} & 130&100.0\% & 2 \\
\hline
\hline
Arc.vdmpp & & 100.0\% & 490 \\
\hline
\end{longtable}
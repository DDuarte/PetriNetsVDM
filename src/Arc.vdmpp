class Arc
  instance variables
    public place: Place
end Arc

class InputArc is subclass of Arc
  operations
    public isEnabled: map Place to nat ==> bool
    isEnabled(marking) == is subclass responsibility;
end InputArc

class ResetArc is subclass of InputArc
  operations
    public ResetArc: Place ==> ResetArc
    ResetArc(p) == (
      place := p;
      return self;
    );

    public isEnabled: map Place to nat ==> bool
    isEnabled(-) == (
      return true;
    );
end ResetArc

class InhibitorArc is subclass of InputArc
  operations
    public InhibitorArc: Place ==> InhibitorArc
    InhibitorArc(p) == (
      place := p;
      return self;
    );

    public isEnabled: map Place to nat ==> bool
    isEnabled(marking) == (
      if marking(place) = 0 then
        return true
      else
        return false;
    );
end InhibitorArc

class WeightedInputArc is subclass of InputArc
  instance variables
    public weight: nat;
  operations
    public WeightedInputArc: Place * nat ==> WeightedInputArc
    WeightedInputArc(p, wght) == (
      place := p;
      weight := wght;
      return self
    );

    public isEnabled: map Place to nat ==> bool
    isEnabled(marking) == (
      return marking(place) >= weight;
    );
end WeightedInputArc

class OutputArc is subclass of Arc

end OutputArc

class WeightedOutputArc is subclass of OutputArc
  instance variables
    public weight: nat;
  operations
    public WeightedOutputArc: Place *nat ==> WeightedOutputArc
    WeightedOutputArc(p, wght) == (
      place := p;
      weight := wght;
      return self
    );

    public isEnabled: map Place to nat ==> bool
    isEnabled(-) == (
      return true;
    );
end WeightedOutputArc

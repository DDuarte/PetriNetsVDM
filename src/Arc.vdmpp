class Arc
  instance variables
    public place: Place
  operations
    public isEnabled: map Place to nat ==> bool
    isEnabled(marking) == is subclass responsibility;
  
    public trigger: Transition * map Place to nat ==> map Place to nat
    trigger(t, m) == is subclass responsibility;
end Arc

class InputArc is subclass of Arc
  operations
    public isEnabled: map Place to nat ==> bool
    isEnabled(marking) == is subclass responsibility;
end InputArc

class ResetArc is subclass of InputArc
  operations
    public ResetArc: Place ==> ResetArc
    ResetArc(p) == (
      place := p;
      return self;
    );

    public isEnabled: map Place to nat ==> bool
    isEnabled(-) == (
      return true;
    );
    
    public trigger: Transition * map Place to nat ==> map Place to nat
    trigger(-, m) == (
      dcl newMarking: map Place to nat := m;
      newMarking(place) := 0;
      return newMarking;
    );
end ResetArc

class InhibitorArc is subclass of InputArc
  operations
    public InhibitorArc: Place ==> InhibitorArc
    InhibitorArc(p) == (
      place := p;
      return self;
    );

    public isEnabled: map Place to nat ==> bool
    isEnabled(marking) == (
      if marking(place) = 0 then
        return true
      else
        return false;
    );

    public trigger: Transition * map Place to nat ==> map Place to nat
    trigger(-, m) == (
      return m;
    );
end InhibitorArc

class WeightedInputArc is subclass of InputArc
  instance variables
    public weight: nat;
  operations
    public WeightedInputArc: Place * nat ==> WeightedInputArc
    WeightedInputArc(p, wght) == (
      place := p;
      weight := wght;
      return self
    );

    public isEnabled: map Place to nat ==> bool
    isEnabled(marking) == (
      return marking(place) >= weight;
    );

    public trigger: Transition * map Place to nat ==> map Place to nat
    trigger(-, m) == (
      dcl newMarking: map Place to nat := m;
      newMarking(place) := newMarking(place) - weight;
      return newMarking;
    );
end WeightedInputArc

class OutputArc is subclass of Arc

end OutputArc

class WeightedOutputArc is subclass of OutputArc
  instance variables
    public weight: nat;
  operations
    public WeightedOutputArc: Place *nat ==> WeightedOutputArc
    WeightedOutputArc(p, wght) == (
      place := p;
      weight := wght;
      return self
    );

    public isEnabled: map Place to nat ==> bool
    isEnabled(-) == (
      return true;
    );

    public trigger: Transition * map Place to nat ==> map Place to nat
    trigger(-, m) == (
      dcl newMarking: map Place to nat := m;
      newMarking(place) := newMarking(place) + weight;
      return newMarking;
    );
end WeightedOutputArc
